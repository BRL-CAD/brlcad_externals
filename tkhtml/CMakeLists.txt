# Unfortunately, there does not appear to be a reliable way to test for the
# presence of the Tkhtml package on a system Tcl/Tk.  We key off of the
# presence of the TK_BLD target, but that may produce a false negative if the
# Tk build is off but we still need Tkhtml.  As far as I can tell the "package
# require Tkhtml" test (which is what is required to properly test for an
# available Tkhtml package) can ONLY be performed successfully on a system that
# supports creation of a graphics window. Window creation isn't typically
# available on continuous integration runners, which means the test will always
# fail there even when it shouldn't.

# We try to find the Tkhtml library, since that's the only test we can do without
# needing the graphical invocation.  Unfortunately, even a find_library search
# looking for libTkhtml isn't completely reliable, since the presence of a shared
# library is not a guarantee it is correctly hooked into the "package require"
# mechanism of the system Tcl/Tk we want to use.  (It is possible to have more
# than one Tcl/Tk on a system - this situation is known to have occurred on the
# Mac when 3rd party package managers are used, for example.)

if (ENABLE_TK AND NOT DEFINED ENABLE_TKHTML)
  set(ENABLE_TKHTML ON)
endif (ENABLE_TK AND NOT DEFINED ENABLE_TKHTML)

if (NOT ENABLE_TKHTML)
  find_library(TKHTML_SYS_LIBRARY NAMES tkhtml Tkhtml Tkhtml3)

  if (NOT TKHTML_SYS_LIBRARY AND NOT DEFINED ENABLE_TKHTML)
    set(ENABLE_TKHTML "ON" CACHE BOOL "Enable Tkhtml build")
  endif (NOT TKHTML_SYS_LIBRARY AND NOT DEFINED ENABLE_TKHTML)

endif (NOT ENABLE_TKHTML)
set(ENABLE_TKHTML "${ENABLE_TKHTML}" CACHE BOOL "Enable Tkhtml build")

if (ENABLE_TKHTML)

  if (TARGET TCL_BLD)
    set(TCL_TARGET TCL_BLD)
  endif (TARGET TCL_BLD)

  if (TARGET TK_BLD)
    set(TK_TARGET TK_BLD)
  endif (TARGET TK_BLD)

  ExternalProject_Add(TKHTML_BLD
    SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/tkhtml"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    ${BUILD_TYPE_SPECIFIER}
    -DBIN_DIR=$<IF:$<BOOL:${WIN32}>,${LIB_DIR},${BIN_DIR}>
    -DLIB_DIR=${LIB_DIR}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${CMAKE_BUNDLE_INSTALL_PREFIX}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUNDLE_INSTALL_PREFIX}/${LIB_DIR}/${RPATH_SUFFIX}
    -DINCLUDE_DIR=${INCLUDE_DIR}
    -DSHARED_DIR=${LIB_DIR}
    -DTCL_ENABLE_TK=ON
    -DTCL_ROOT=$<$<BOOL:${TCL_TARGET}>:${CMAKE_BUNDLE_INSTALL_PREFIX}>
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    STEP_TARGETS install
    )

  ExternalProject_Add_StepTargets(TKHTML_BLD install)

  if (TARGET PATCH_BLD)
    ExternalProject_Add_StepDependencies(TKHTML_BLD configure PATCH_BLD-install)
  endif (TARGET PATCH_BLD)

  if (TARGET TCL_BLD)
    ExternalProject_Add_StepDependencies(TKHTML_BLD configure TCL_BLD-install)
  endif (TARGET TCL_BLD)

  if (TARGET TK_BLD)
    ExternalProject_Add_StepDependencies(TKHTML_BLD configure TK_BLD-install)
  endif (TARGET TK_BLD)

endif (ENABLE_TKHTML)

# Copy the license into position in CMAKE_BUNDLE_INSTALL_PREFIX
file(MAKE_DIRECTORY ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other)
configure_file(
  ${CMAKE_CURRENT_SOURCE_DIR}/tkhtml/COPYING
  ${CMAKE_BUNDLE_INSTALL_PREFIX}/doc/legal/other/tkhtml.txt
  COPYONLY
  )

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

