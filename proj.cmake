set(proj_DESCRIPTION "
Option for enabling and disabling compilation of the PROJ geographic
projection library provided with BRL-CAD's source code.  Default
is AUTO, responsive to the toplevel BRLCAD_BUNDLED_LIBS option and
testing first for a system version if BRLCAD_BUNDLED_LIBS is also
AUTO.
")
THIRD_PARTY(proj PROJ proj
  proj_DESCRIPTION
  ALIASES ENABLE_PROJ
  RESET_VARS PROJ_LIBRARY PROJ_LIBRARIES PROJ_INCLUDE_DIR PROJ_INCLUDE_DIRS
  )

if (BRLCAD_PROJ_BUILD)

  if (TARGET SQLITE3_BLD)
    set(SQLITE3_TARGET SQLITE3_BLD)
    set(SQLite3_EXECNAME ${CMAKE_BINARY_INSTALL_ROOT}/${BIN_DIR}/sqlite3${CMAKE_EXECUTABLE_SUFFIX})
  endif (TARGET SQLITE3_BLD)

  #set(PROJ_INSTDIR ${CMAKE_BINARY_INSTALL_ROOT}/proj)
  set(PROJ_INSTDIR ${CMAKE_BINARY_INSTALL_ROOT})

  ExternalProject_Add(PROJ_BLD
    SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/proj"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    $<$<NOT:$<BOOL:${CMAKE_CONFIGURATION_TYPES}>>:-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}>
    -DBIN_DIR=${BIN_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${PROJ_INSTDIR}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUILD_RPATH}
    -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=${CMAKE_INSTALL_RPATH_USE_LINK_PATH}
    -DCMAKE_SKIP_BUILD_RPATH=${CMAKE_SKIP_BUILD_RPATH}
    -DLIB_DIR=${LIB_DIR}
    -DCMAKE_INSTALL_LIBDIR=${LIB_DIR}
    -DPROJ_LIB_DIR=${CMAKE_INSTALL_PREFIX}/${DATA_DIR}/proj
    -DSQLite3_ROOT=$<$<BOOL:${SQLITE3_TARGET}>:${CMAKE_BINARY_INSTALL_ROOT}>
    -DEXE_SQLITE3=${SQLite3_EXECNAME}
    -DBUILD_TESTING=OFF
    -DENABLE_TIFF=OFF
    -DENABLE_CURL=OFF
    -DBUILD_PROJSYNC=OFF
    DEPENDS ${PROJ_DEPS}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    )

  if (TARGET SQLITE3_BLD)
    ExternalProject_Add_StepDependencies(PROJ_BLD configure SQLITE3_BLD-install)
  endif (TARGET SQLITE3_BLD)

  ExternalProject_Add_StepTargets(PROJ_BLD install)

  SetTargetFolder(PROJ_BLD "Third Party Libraries")
  SetTargetFolder(proj "Third Party Libraries")

endif (BRLCAD_PROJ_BUILD)

mark_as_advanced(PROJ_INCLUDE_DIRS)
mark_as_advanced(PROJ_LIBRARIES)

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

