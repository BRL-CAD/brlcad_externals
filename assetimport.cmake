set(assetimport_DESCRIPTION "
Option for enabling and disabling compilation of the Open
Asset Import Library provided with BRL-CAD's source code.  Default
is AUTO, responsive to the toplevel BRLCAD_BUNDLED_LIBS option and
testing first for a system version if BRLCAD_BUNDLED_LIBS is also
AUTO.
")
THIRD_PARTY(assetimport ASSETIMPORT assetimport
  assetimport_DESCRIPTION
  ALIASES ENABLE_ASSETIMPORT
  RESET_VARS ASSETIMPORT_LIBRARY ASSETIMPORT_LIBRARIES ASSETIMPORT_INCLUDE_DIR ASSETIMPORT_INCLUDE_DIRS
  )

if (BRLCAD_ASSETIMPORT_BUILD)

  set(ASSETIMPORT_DEPS)
  set(TARGET_LIST zlib)
  foreach(T ${TARGET_LIST})
    if (TARGET ${T}_stage)
      list(APPEND ASSETIMPORT_DEPS ${T}_stage)
    endif (TARGET ${T}_stage)
  endforeach(T ${TARGET_LIST})

  if (TARGET ZLIB_BLD)
    set(ZLIB_TARGET ZLIB_BLD)

    if (MSVC)
      set(ZLIB_LIBRARY ${CMAKE_BINARY_INSTALL_ROOT}/${LIB_DIR}/${ZLIB_BASENAME}.lib)
    elseif (OPENBSD)
      set(ZLIB_LIBRARY ${CMAKE_BINARY_INSTALL_ROOT}/${LIB_DIR}/${ZLIB_BASENAME}${CMAKE_SHARED_LIBRARY_SUFFIX}.1.2)
    else (MSVC)
      set(ZLIB_LIBRARY ${CMAKE_BINARY_INSTALL_ROOT}/${LIB_DIR}/${ZLIB_BASENAME}${CMAKE_SHARED_LIBRARY_SUFFIX})
    endif (MSVC)

    set(ZLIB_INCLUDE_DIR ${CMAKE_BINARY_INSTALL_ROOT}/include)
  endif (TARGET ZLIB_BLD)

  if (MSVC)
    set(ASSETIMPORT_BASENAME "assimp-vc${MSVC_TOOLSET_VERSION}-mt")
  else (MSVC)
    set(ASSETIMPORT_BASENAME assimp)
  endif (MSVC)
  set_lib_vars(ASSETIMPORT ${ASSETIMPORT_BASENAME} "5" "2" "4")

  #set(ASSETIMPORT_INSTDIR ${CMAKE_BINARY_INSTALL_ROOT}/assetimport)
  set(ASSETIMPORT_INSTDIR ${CMAKE_BINARY_INSTALL_ROOT})

  ExternalProject_Add(ASSETIMPORT_BLD
    SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/assetimport"
    BUILD_ALWAYS ${EXT_BUILD_ALWAYS} ${LOG_OPTS}
    CMAKE_ARGS
    $<$<NOT:$<BOOL:${CMAKE_CONFIGURATION_TYPES}>>:-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}>
    -DBIN_DIR=${BIN_DIR}
    -DBUILD_STATIC_LIBS=${BUILD_STATIC_LIBS}
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
    -DCMAKE_INSTALL_PREFIX=${ASSETIMPORT_INSTDIR}
    -DCMAKE_INSTALL_RPATH=${CMAKE_BUILD_RPATH}
    -DCMAKE_INSTALL_RPATH_USE_LINK_PATH=${CMAKE_INSTALL_RPATH_USE_LINK_PATH}
    -DCMAKE_SKIP_BUILD_RPATH=${CMAKE_SKIP_BUILD_RPATH}
    -DLIB_DIR=${LIB_DIR}
    -DCMAKE_INSTALL_LIBDIR=${LIB_DIR}
    -DZLIB_LIBRARY=$<$<BOOL:${ZLIB_TARGET}>:${ZLIB_LIBRARY}>
    -DZLIB_LIBRARIES=$<$<BOOL:${ZLIB_TARGET}>:${ZLIB_LIBRARY}>
    -DZLIB_LIBRARY_DBG=$<$<BOOL:${ZLIB_TARGET}>:${ZLIB_LIBRARY}>
    -DZLIB_LIBRARY_REL=$<$<BOOL:${ZLIB_TARGET}>:${ZLIB_LIBRARY}>
    -DZLIB_INCLUDE_DIR=$<$<BOOL:${ZLIB_TARGET}>:${ZLIB_INCLUDE_DIR}>
    -DZLIB_ROOT=${CMAKE_BINARY_INSTALL_ROOT}
    -DASSIMP_BUILD_ASSIMP_TOOLS=OFF
    -DASSIMP_BUILD_TESTS=OFF
    -DASSIMP_BUILD_MINIZIP=ON
    -DASSIMP_INJECT_DEBUG_POSTFIX=OFF
    -DASSIMP_INSTALL_PDB=OFF
    DEPENDS ${ASSETIMPORT_DEPS}
    LOG_CONFIGURE ${EXT_BUILD_QUIET}
    LOG_BUILD ${EXT_BUILD_QUIET}
    LOG_INSTALL ${EXT_BUILD_QUIET}
    LOG_OUTPUT_ON_FAILURE ${EXT_BUILD_QUIET}
    )

  SetTargetFolder(ASSETIMPORT_BLD "Third Party Libraries")
  SetTargetFolder(assetimport "Third Party Libraries")

  DISTCLEAN("${CMAKE_CURRENT_BINARY_DIR}/ASSETIMPORT_BLD-prefix")

endif (BRLCAD_ASSETIMPORT_BUILD)

mark_as_advanced(ASSETIMPORT_INCLUDE_DIRS)
mark_as_advanced(ASSETIMPORT_LIBRARIES)

include("${CMAKE_CURRENT_SOURCE_DIR}/assetimport.dist")

# Local Variables:
# tab-width: 8
# mode: cmake
# indent-tabs-mode: t
# End:
# ex: shiftwidth=2 tabstop=8

